plugins {
    id "net.ltgt.errorprone" version "0.0.13"
    id "com.jfrog.bintray" version "1.8.0"
    id 'net.researchgate.release' version '2.7.0'
}

repositories {
    mavenCentral()
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'java-library'
}

sourceCompatibility = 1.8

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.297'
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name:'mockito-core', version:'2.16.0'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'org.testcontainers', name: 'localstack', version: '1.7.1'

}

publishing {
    publications {
        SchemaRegistryPublication(MavenPublication) {
            from components.java

            groupId 'de.khamrakulov.schema-registry'
            artifactId 'registry-core'
            version project.property('version')

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                asNode().appendNode('description', 'AWS S3 based schema registry with support of Avro')
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

release {
    git {
        requireBranch = '(master|\\d+\\.\\d+)'
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['SchemaRegistryPublication']
    pkg {
        repo = 'maven'
        name = 's3-schema-registry'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/htimur/s3-schema-registry.git'
        labels = ['avro',
                  'schema',
                  'registry',
                  'schemas',
                  'schema-registry',
                  's3',
                  'aws']
        publicDownloadNumbers = true
    }
}